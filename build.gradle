plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.luna'
version = '0.0.1-SNAPSHOT'
description = 'Luna - A modern social media platform'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

tasks.withType(JavaCompile) {
	options.compilerArgs << '-parameters'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'me.paulschwarz:spring-dotenv:4.0.0'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	implementation 'org.springframework.boot:spring-boot-starter-flyway'
	implementation 'org.flywaydb:flyway-database-postgresql'
	implementation 'com.bucket4j:bucket4j-core:8.10.1'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
	implementation 'com.cloudinary:cloudinary-http44:1.39.0'
	implementation 'com.google.api-client:google-api-client:2.7.2'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'com.google.firebase:firebase-admin:9.3.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-validation-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	
	// Load .env file if it exists
	def envFile = file('.env')
	if (envFile.exists()) {
		envFile.readLines().each { line ->
			if (line && !line.startsWith('#') && line.contains('=')) {
				def parts = line.split('=', 2)
				if (parts.length == 2) {
					def key = parts[0].trim()
					def value = parts[1].trim()
					// Remove quotes if present
					if (value.startsWith('"') && value.endsWith('"')) {
						value = value.substring(1, value.length() - 1)
					}
					environment key, value
				}
			}
		}
	}
}
